PROJ := 'hh-gtsf-monitor'

# ECR repo and tags
ECR_REPO := "286712564201.dkr.ecr.eu-west-1.amazonaws.com/research"
LATEST_TAG := PROJ + '-latest'
DATE_TAG := PROJ + '-' + shell('date -I')


# Display commands
@help:
    just --list


# Set up repo
@init:
    echo "3.8" > .python-version
    uv sync


# Build image
@build:
    tar -czhf ui.tar.gz --dereference ui-dist
    docker build -t {{PROJ}} .
    

# Run the docker container locally
@run:
    docker run \
        -ti \
        --rm \
        --network host \
        {{PROJ}} /bin/bash


# Release the image to ECR
@release: build
    for tag in {{LATEST_TAG}} {{DATE_TAG}}; do \
        echo Pushing ${tag}...; \
        docker tag {{PROJ}} "{{ECR_REPO}}:${tag}"; \
        docker push "{{ECR_REPO}}:${tag}"; \
    done
